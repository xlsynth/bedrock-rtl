# SPDX-License-Identifier: Apache-2.0

load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@stardoc//stardoc:stardoc.bzl", "stardoc")
load("//bazel:verilog.bzl", "runner_flags")

package(default_visibility = ["//visibility:public"])

bzl_library(
    name = "rules_hdl_providers",
    srcs = ["@rules_hdl//verilog:providers.bzl"],
)

stardoc(
    name = "gen_verilog_rules",
    out = "gen_verilog_rules.md",
    input = "verilog.bzl",
    deps = [
        ":rules_hdl_providers",
        "@bazel_skylib//rules:write_file",
    ],
)

diff_test(
    name = "verilog_rules_diff_test",
    failure_message = """
        Golden verilog_rules.md does not match the generated documentation.
        Please run `bazel run //bazel:update_docs` to automatically update the golden documentation.
    """,
    file1 = "verilog_rules.md",
    file2 = ":gen_verilog_rules.md",
)

write_file(
    name = "gen_update_docs",
    out = "update_docs.sh",
    content = [
        "#!/usr/bin/env bash",
        "cd $BUILD_WORKSPACE_DIRECTORY",
        "cp -fv bazel-bin/bazel/gen_verilog_rules.md bazel/verilog_rules.md",
    ],
)

sh_binary(
    name = "update_docs",
    srcs = [":update_docs.sh"],
    data = [
        "verilog_rules.md",
        ":gen_verilog_rules.md",
    ],
)

runner_flags(
    name = "runner_flags",
    build_setting_default = "",
)
