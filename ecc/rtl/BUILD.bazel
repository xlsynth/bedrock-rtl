# Copyright 2024-2025 The Bedrock-RTL Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@rules_hdl//verilog:providers.bzl", "verilog_library")
load("//bazel:br_verilog.bzl", "br_verilog_elab_and_lint_test_suite")

package(default_visibility = ["//visibility:public"])

#########################################
# SED Encoder
#########################################
verilog_library(
    name = "br_ecc_sed_encoder",
    srcs = ["br_ecc_sed_encoder.sv"],
    deps = [
        "//delay/rtl:br_delay_valid",
        "//macros:br_asserts_internal",
    ],
)

br_verilog_elab_and_lint_test_suite(
    name = "br_ecc_sed_encoder_test_suite",
    params = {"DataWidth": [
        "1",
        "32",
    ]},
    deps = [":br_ecc_sed_encoder"],
)

#########################################
# SED Decoder
#########################################
verilog_library(
    name = "br_ecc_sed_decoder",
    srcs = ["br_ecc_sed_decoder.sv"],
    deps = [
        "//delay/rtl:br_delay_valid",
        "//macros:br_asserts_internal",
    ],
)

br_verilog_elab_and_lint_test_suite(
    name = "br_ecc_sed_decoder_test_suite",
    params = {"DataWidth": [
        "1",
        "32",
    ]},
    deps = [":br_ecc_sed_decoder"],
)

#########################################
# SECDED Encoder
#########################################
verilog_library(
    name = "br_ecc_secded_encoder",
    srcs = ["br_ecc_secded_encoder.sv"],
    deps = [
        "//delay/rtl:br_delay_valid",
        "//macros:br_asserts",
        "//macros:br_asserts_internal",
        "//macros:br_assign",
        "//macros:br_unused",
        "//pkg:br_ecc_pkg",
        "//pkg:br_math_pkg",
    ],
)

K_R_PAIRS = [
    # Max K per R
    ("4", "4"),
    ("11", "5"),
    ("26", "6"),
    ("57", "7"),
    ("120", "8"),
    ("247", "9"),
    ("502", "10"),
    ("1013", "11"),
    ("2036", "12"),
    # Less than max K per R
    ("8", "5"),
    ("64", "8"),
]

[
    br_verilog_elab_and_lint_test_suite(
        name = "br_ecc_secded_encoder_test_suite_" + k + "_" + r,
        params = {
            "DataWidth": [k],
            "ParityWidth": [r],
            "RegisterOutputs": [
                "0",
                "1",
            ],
        },
        deps = [":br_ecc_secded_encoder"],
    )
    for (k, r) in K_R_PAIRS
]

diff_test(
    name = "br_ecc_secded_encoder_diff_test",
    failure_message = """
        Golden br_ecc_secded_encoder.sv does not match the generated code.
        Please run `bazel run //ecc/rtl:update_br_ecc_secded_encoder` to automatically update the golden.
    """,
    file1 = "br_ecc_secded_encoder.sv",
    file2 = ":gen_br_ecc_secded_encoder.sv",
)

genrule(
    name = "gen_br_ecc_secded_encoder",
    srcs = ["//python/eccgen:br_ecc_secded_encoder.sv.jinja2"],
    outs = ["gen_br_ecc_secded_encoder.sv"],
    cmd = "$(location //python/eccgen:eccgen) --scheme=hsiao_secded --rtl-encoder-template=$(location //python/eccgen:br_ecc_secded_encoder.sv.jinja2) --rtl-encoder-output=$@",
    tools = ["//python/eccgen"],
)

write_file(
    name = "gen_update_br_ecc_secded_encoder",
    out = "update_br_ecc_secded_encoder.sh",
    content = [
        "#!/usr/bin/env bash",
        "cd $BUILD_WORKSPACE_DIRECTORY",
        "cp -fv bazel-bin/ecc/rtl/gen_br_ecc_secded_encoder.sv ecc/rtl/br_ecc_secded_encoder.sv",
    ],
)

sh_binary(
    name = "update_br_ecc_secded_encoder",
    srcs = [":update_br_ecc_secded_encoder.sh"],
    data = [
        "br_ecc_secded_encoder.sv",
        ":gen_br_ecc_secded_encoder.sv",
    ],
)

#########################################
# SECDED Decoder
#########################################
verilog_library(
    name = "br_ecc_secded_decoder",
    srcs = ["br_ecc_secded_decoder.sv"],
    deps = [
        "//delay/rtl:br_delay_valid",
        "//macros:br_asserts",
        "//macros:br_asserts_internal",
        "//macros:br_assign",
        "//macros:br_unused",
        "//pkg:br_ecc_pkg",
        "//pkg:br_math_pkg",
    ],
)

[
    br_verilog_elab_and_lint_test_suite(
        name = "br_ecc_secded_decoder_test_suite_" + k + "_" + r,
        params = {
            "DataWidth": [k],
            "ParityWidth": [r],
            "RegisterOutputs": [
                "0",
                "1",
            ],
        },
        deps = [":br_ecc_secded_encoder"],
    )
    for (k, r) in K_R_PAIRS
]

diff_test(
    name = "br_ecc_secded_decoder_diff_test",
    failure_message = """
        Golden br_ecc_secded_decoder.sv does not match the generated code.
        Please run `bazel run //ecc/rtl:update_br_ecc_secded_decoder` to automatically update the golden.
    """,
    file1 = "br_ecc_secded_decoder.sv",
    file2 = ":gen_br_ecc_secded_decoder.sv",
)

genrule(
    name = "gen_br_ecc_secded_decoder",
    srcs = ["//python/eccgen:br_ecc_secded_decoder.sv.jinja2"],
    outs = ["gen_br_ecc_secded_decoder.sv"],
    cmd = "$(location //python/eccgen:eccgen) --scheme=hsiao_secded --rtl-decoder-template=$(location //python/eccgen:br_ecc_secded_decoder.sv.jinja2) --rtl-decoder-output=$@",
    tools = ["//python/eccgen"],
)

write_file(
    name = "gen_update_br_ecc_secded_decoder",
    out = "update_br_ecc_secded_decoder.sh",
    content = [
        "#!/usr/bin/env bash",
        "cd $BUILD_WORKSPACE_DIRECTORY",
        "cp -fv bazel-bin/ecc/rtl/gen_br_ecc_secded_decoder.sv ecc/rtl/br_ecc_secded_decoder.sv",
    ],
)

sh_binary(
    name = "update_br_ecc_secded_decoder",
    srcs = [":update_br_ecc_secded_decoder.sh"],
    data = [
        "br_ecc_secded_decoder.sv",
        ":gen_br_ecc_secded_decoder.sv",
    ],
)
